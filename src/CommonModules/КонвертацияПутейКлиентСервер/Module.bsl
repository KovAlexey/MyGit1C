
#Область ПрограммныйИнтерфейс

Функция ПутьEDTВПутьXML(ОтносительныйПуть) Экспорт
	РазобранныйПуть = СтрРазделить(ОтносительныйПуть, "/", Ложь);
	МассивРезультат = Новый Массив;
	Результат = "";
	
	Граница = РазобранныйПуть.ВГраница();
	
	Если РазобранныйПуть.Количество() = 2 Тогда
		Если РазобранныйПуть[1] = "Configuration.mdo" Тогда
			МассивРезультат.Добавить("Configuration.xml");
		КонецЕсли;
		
	ИначеЕсли РазобранныйПуть.Количество() = 3 Тогда
		
		//Общие модули, непосредственно объекты, кроме подсистем
		Расширение = Прав(РазобранныйПуть[2], 4);
		Если Расширение = ".bsl" Тогда
			//Модуль, например общий
			Для Сч = 0 По 1 Цикл
				МассивРезультат.Добавить(РазобранныйПуть[Сч]);
			КонецЦикла;
			МассивРезультат.Добавить("Ext");
			МассивРезультат.Добавить(РазобранныйПуть[Сч]);

		ИначеЕсли Расширение = ".mdo" Тогда
			МассивРезультат.Добавить(РазобранныйПуть[0]);
			МассивРезультат.Добавить(СтрЗаменить(РазобранныйПуть[2], ".mdo", ".xml"));
		ИначеЕсли СтрЗаканчиваетсяНа(РазобранныйПуть[РазобранныйПуть.ВГраница()], ".rights") Тогда
			МассивРезультат.Добавить("Roles");
			ИмяРоли = РазобранныйПуть[РазобранныйПуть.ВГраница() - 1];
			МассивРезультат.Добавить(ИмяРоли);
			МассивРезультат.Добавить("Ext");
			МассивРезультат.Добавить("Rights.xml");
		ИначеЕсли СтрЗаканчиваетсяНа(РазобранныйПуть[РазобранныйПуть.ВГраница()], ".xdto") Тогда
			МассивРезультат.Добавить(РазобранныйПуть[0]); // XDTOPackages
			МассивРезультат.Добавить(РазобранныйПуть[1]); // PackageName
			МассивРезультат.Добавить("Ext");
			МассивРезультат.Добавить("Package.bin");
		КонецЕсли;
		
	ИначеЕсли РазобранныйПуть.Количество() > 3 Тогда
		
		Если РазобранныйПуть[2] = "Forms" Тогда
			//Формы
			Расширение = Прав(РазобранныйПуть[РазобранныйПуть.ВГраница()], 4);
			
			// Модуль
			Если Расширение = ".bsl"
			Или Прав(РазобранныйПуть[РазобранныйПуть.ВГраница()], 5) = ".form" Тогда
			
				Для Сч = 0 По РазобранныйПуть.ВГраница() - 2 Цикл
					МассивРезультат.Добавить(РазобранныйПуть[Сч]);
				КонецЦикла;
				МассивРезультат.Добавить(СтрШаблон("%1.xml", РазобранныйПуть[Граница - 1]));
			КонецЕсли;
			
		ИначеЕсли Прав(РазобранныйПуть[РазобранныйПуть.ВГраница()], 4) = ".dcs" Тогда
			Для Сч = 0 По РазобранныйПуть.ВГраница() - 2 Цикл
				МассивРезультат.Добавить(РазобранныйПуть[Сч]);
			КонецЦикла;
			МассивРезультат.Добавить(СтрШаблон("%1.xml", РазобранныйПуть[Граница - 1]));
			
		ИначеЕсли РазобранныйПуть[2] = "Subsystems" Тогда
			Расширение = Прав(РазобранныйПуть[РазобранныйПуть.ВГраница()], 4);
			
			Для Сч = 0 По Граница - 2 Цикл
				МассивРезультат.Добавить(РазобранныйПуть[Сч]);
			КонецЦикла;
			
			Если Расширение = ".mdo" Тогда
				МассивРезультат.Добавить(СтрШаблон("%1.xml", РазобранныйПуть[Граница - 1]));
			ИначеЕсли Расширение = ".cmi" Тогда
				МассивРезультат.Добавить("Ext");
				МассивРезультат.Добавить(СтрЗаменить(РазобранныйПуть[РазобранныйПуть.ВГраница()], ".cmi", ".xml"));
			КонецЕсли;
		ИначеЕсли РазобранныйПуть[2] = "Templates" Тогда
			Для Сч = 0 По Граница - 2 Цикл
				МассивРезультат.Добавить(РазобранныйПуть[Сч]);
			КонецЦикла;
			
			ИмяФайла = СтрШаблон("%1.xml", РазобранныйПуть[Граница - 1]);
			МассивРезультат.Добавить(ИмяФайла);
		КонецЕсли;
			
	КонецЕсли;
	
	Результат = СтрСоединить(МассивРезультат, "/");
	
	Если ПустаяСтрока(Результат) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("Для %1 не удалось сформировать путь", ОтносительныйПуть));
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

Функция ПутьКМетаданнымВПутьXML(ПутьКМетаданным) Экспорт
	СоответствиеИменаМетаданныхПапки = РаботаСГитомКлиентСервер.ПолучитьСоответствияМетаданныхПапкам();
	
	РазделенныйПуть = СтрРазделить(ПутьКМетаданным, ".");
	Если РазделенныйПуть.Количество() = 1
	И РазделенныйПуть[0] = "Configuration" Тогда
		Возврат ПутьККорнюКонфигурации();
	ИначеЕсли РазделенныйПуть.Количество() = 2 Тогда // Отдельный объект
		Возврат ПутьКОбъектуXML(РазделенныйПуть);
	Иначе
		Возврат ПутьКОбъектуXMLПроизвольный(РазделенныйПуть);
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

Функция ПутьККорнюКонфигурации()
	Возврат "Configuration.xml";
КонецФункции

Функция ПутьКОбъектуXML(РазделенныйПутьПутьКМетаданным)
	Соответствие = РаботаСГитомКлиентСервер.ПолучитьСоответствияМетаданныхПапкам();
	
	ПутьККаталогуМассив = Новый Массив;
	
	ВидМетаданного = РазделенныйПутьПутьКМетаданным[0];
	Каталог = Соответствие[ВидМетаданного];
	Если Не ЗначениеЗаполнено(Каталог) Тогда
		ВызватьИсключение СтрШаблон("Вид метаданного ""%1"" не найден!", Каталог);
	КонецЕсли;
	
	ПутьККаталогуМассив.Добавить(Каталог);
	ИмяОбъекта = РазделенныйПутьПутьКМетаданным[1];
	
	ПутьККаталогуМассив.Добавить(СтрШаблон("%1.xml", ИмяОбъекта));
	
	Возврат СтрСоединить(ПутьККаталогуМассив, "/");
	
КонецФункции

Функция ПутьКОбъектуXMLПроизвольный(РазделенныйПутьПутьКМетаданным)
	Граница = РазделенныйПутьПутьКМетаданным.ВГраница();
	
	КонечныйОбъект = РазделенныйПутьПутьКМетаданным[Граница];
	Если СтрЗаканчиваетсяНа(КонечныйОбъект, "Module") Тогда
		Формат = "bsl";
	ИначеЕсли КонечныйОбъект = "Package" Тогда
		Формат = "bin";
	Иначе
		Формат = "xml";
	КонецЕсли;
	
	Если КонечныйОбъект = "Form" Тогда
		Граница = Граница - 1;
	КонецЕсли;
	
	Соответствие = РаботаСГитомКлиентСервер.ПолучитьСоответствияМетаданныхПапкам();
	
	ИтоговыйПутьМассив = Новый Массив;
	Для Сч = 0 По Граница Цикл
		НачальныйПуть = РазделенныйПутьПутьКМетаданным[Сч];
		КонечныйЭлемент = (Сч = Граница);
		Если Сч % 2 = 0 Тогда
			Если СтрЗаканчиваетсяНа(НачальныйПуть, "Module")
			И КонечныйЭлемент Тогда
				ИтоговыйПуть = СтрШаблон("Ext/%1", НачальныйПуть);
			ИначеЕсли КонечныйЭлемент
			И НачальныйПуть = "Package" Тогда
				ИтоговыйПуть = "Ext/Package";
			ИначеЕсли НачальныйПуть = "Rights" Тогда
				ИтоговыйПутьМассив.Добавить("Ext");
				ИтоговыйПуть = НачальныйПуть;
			Иначе
				ИтоговыйПуть = Соответствие[НачальныйПуть];
				Если Не ЗначениеЗаполнено(ИтоговыйПуть) Тогда
					ВызватьИсключение СтрШаблон("Не найдено соответствие поля ""%1"".", НачальныйПуть);
				КонецЕсли;
			КонецЕсли;
		Иначе
			ИтоговыйПуть = НачальныйПуть;
		КонецЕсли;
		
		Если Сч = Граница Тогда
			ИтоговыйПуть = СтрШаблон("%1.%2", ИтоговыйПуть, Формат);
		КонецЕсли;
		ИтоговыйПутьМассив.Добавить(ИтоговыйПуть);
	КонецЦикла;
	
	Возврат СтрСоединить(ИтоговыйПутьМассив, "/");
КонецФункции

#КонецОбласти
