			   


Функция РазобратьСтрокуНаМетаданные(ИмяФайла, СоответствиеПапкаМетаданные) Экспорт
	Результат = Новый Структура("ТипМетаданных, ИмяМетаданного, ПолныйПутьКМетаданному");
	
	СоответствиеПапкаМетаданные = ПолучитьСоответствияМетаданныхПапкам();
	
	РазделеннаяСтрока = СтрРазделить(ИмяФайла, "/", Ложь);
	
	ТипМетаданныхПапка = РазделеннаяСтрока[0];
	ТипМетаданныхИмя = СоответствиеПапкаМетаданные[ТипМетаданныхПапка];
	
	Результат.ТипМетаданных = ТипМетаданныхИмя;
	Если ТипМетаданныхПапка = "Configuration" Тогда
		Результат.ПолныйПутьКМетаданному = "Configuration";
	ИначеЕсли ТипМетаданныхИмя = "Subsystem" Тогда
		Сч = 1;
		ПолныйПуть = "";
		Шаблон = ".Subsystem.%1";
		Пока Сч <= РазделеннаяСтрока.ВГраница() Цикл
			Если РазделеннаяСтрока[Сч - 1] <> ТипМетаданныхПапка Тогда
				Прервать;
			КонецЕсли;
			
			ПолныйПуть = ПолныйПуть + СтрШаблон(Шаблон, РазделеннаяСтрока[Сч]);
			Сч = Сч + 2;
		КонецЦикла;
		ПолныйПуть = Сред(ПолныйПуть, 2);

		Результат.ПолныйПутьКМетаданному = ПолныйПуть;
	Иначе
		Результат.ТипМетаданных = ТипМетаданныхИмя;
		Результат.ИмяМетаданного = РазделеннаяСтрока[1];
		Результат.ПолныйПутьКМетаданному = Результат.ТипМетаданных + "." + Результат.ИмяМетаданного;
	КонецЕсли;
	
	Если РазделеннаяСтрока.Количество() > 3 Тогда
		ТипМетаданныхПапка = РазделеннаяСтрока[2];
		ТипМетаданныхИмя = СоответствиеПапкаМетаданные[ТипМетаданныхПапка];
		Если ЗначениеЗаполнено(ТипМетаданныхИмя) Тогда
			Если ТипМетаданныхИмя = "Form"
			ИЛИ ТипМетаданныхИмя = "Template" Тогда
				ИмяОбъекта = РазделеннаяСтрока[3];
				Результат.ПолныйПутьКМетаданному = Результат.ПолныйПутьКМетаданному + "." + ТипМетаданныхИмя + "." + ИмяОбъекта;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли РазделеннаяСтрока.Количество() = 3 Тогда
		ПоследнийЭлемент = РазделеннаяСтрока[2];
		Если Прав(ПоследнийЭлемент, 3) = "bsl" Тогда
			ДлинаБезРасширения = СтрДлина(ПоследнийЭлемент) - СтрДлина(".bsl");
			Результат.ПолныйПутьКМетаданному = Результат.ПолныйПутьКМетаданному + "." + Лев(ПоследнийЭлемент, ДлинаБезРасширения);
		КонецЕсли;
	КонецЕсли;
	
	
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСоответствияМетаданныхПапкам() Экспорт
	Соответствие = Новый Соответствие;
	
	ДобавитьСоответствие(Соответствие, "WSReferences", "");
	ДобавитьСоответствие(Соответствие, "WebServices", "WebService");
	ДобавитьСоответствие(Соответствие, "Tasks", "Task");
	ДобавитьСоответствие(Соответствие, "SettingsStorages", "SettingsStorage");
	ДобавитьСоответствие(Соответствие, "ScheduledJobs", "ScheduledJob");
	ДобавитьСоответствие(Соответствие, "Reports", "Report");
	ДобавитьСоответствие(Соответствие, "Subsystems", "Subsystem");
	ДобавитьСоответствие(Соответствие, "InformationRegisters", "InformationRegister");
	ДобавитьСоответствие(Соответствие, "FunctionalOptionsParameters", "FunctionalOptionsParameter");
	ДобавитьСоответствие(Соответствие, "DocumentNumerators", "DocumentNumerator");
	ДобавитьСоответствие(Соответствие, "DocumentJournals", "DocumentJournal");
	ДобавитьСоответствие(Соответствие, "EventSubscriptions", "EventSubscription");
	ДобавитьСоответствие(Соответствие, "DefinedTypes", "DefinedType");
	ДобавитьСоответствие(Соответствие, "CommonTemplates", "CommonTemplate");
	ДобавитьСоответствие(Соответствие, "HTTPServices", "HTTPService");
	ДобавитьСоответствие(Соответствие, "Roles", "Role");
	ДобавитьСоответствие(Соответствие, "CommonModules", "CommonModule");
	ДобавитьСоответствие(Соответствие, "FilterCriteria", "FilterCriterion");
	ДобавитьСоответствие(Соответствие, "Enums", "Enum");
	ДобавитьСоответствие(Соответствие, "CommonForms", "CommonForm");
	ДобавитьСоответствие(Соответствие, "CommonCommands", "CommonCommand");
	ДобавитьСоответствие(Соответствие, "CommandGroups", "CommandGroup");
	ДобавитьСоответствие(Соответствие, "FunctionalOptions", "FunctionalOption");
	ДобавитьСоответствие(Соответствие, "ChartsOfCharacteristicTypes", "ChartOfCharacteristicTypes");
	ДобавитьСоответствие(Соответствие, "ChartsOfCalculationTypes", "ChartOfCalculationTypes");
	ДобавитьСоответствие(Соответствие, "ChartsOfAccounts", "ChartOfAccounts");
	ДобавитьСоответствие(Соответствие, "DataProcessors", "DataProcessor");
	ДобавитьСоответствие(Соответствие, "Constants", "Constant");
	ДобавитьСоответствие(Соответствие, "CommonPictures", "CommonPicture");
	ДобавитьСоответствие(Соответствие, "Catalogs", "Catalog");
	ДобавитьСоответствие(Соответствие, "CommonAttributes", "CommonAttribute");
	ДобавитьСоответствие(Соответствие, "Documents", "Document");
	ДобавитьСоответствие(Соответствие, "ExchangePlans", "ExchangePlan");
	ДобавитьСоответствие(Соответствие, "StyleItems", "StyleItem");
	ДобавитьСоответствие(Соответствие, "BusinessProcesses", "BusinessProcess");
	ДобавитьСоответствие(Соответствие, "CalculationRegisters", "CalculationRegister");
	ДобавитьСоответствие(Соответствие, "AccountingRegisters", "AccountingRegister");
	ДобавитьСоответствие(Соответствие, "SessionParameters", "SessionParameter");
	ДобавитьСоответствие(Соответствие, "AccumulationRegisters", "AccumulationRegister");
	ДобавитьСоответствие(Соответствие, "XDTOPackages", "XDTOPackage");
	ДобавитьСоответствие(Соответствие, "Forms", "Form");
	ДобавитьСоответствие(Соответствие, "Templates", "Template");
	
	Возврат Соответствие;
	
КонецФункции

Процедура ДобавитьСоответствие(Соответствие, Множественное, Единственное)
	Соответствие[Множественное] = Единственное;
	Соответствие[Единственное] = Множественное;
КонецПроцедуры       

Функция ПолучитьСписокИзменений(Гит, Идентификатор) Экспорт
	ГруппаКоманд = Новый Массив();
	ГруппаКоманд.Добавить("cmd /C chcp 65001");
	
	МассивКомандаГит = Новый Массив();
	МассивКомандаГит.Добавить("git show  --pretty="""" --name-only");
	МассивКомандаГит.Добавить(Идентификатор);
	
	КомандаГит = СтрСоединить(МассивКомандаГит, " ");
	ГруппаКоманд.Добавить(КомандаГит);
	
	КомандаДляЗапуска = СтрСоединить(ГруппаКоманд, " && ");
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("txt");
	
	Команда = КомандаДляЗапуска + " > " + ИмяВременногоФайла;
	ПараметрыГита = РаботаСГитом.ПолучитьОписаниеХранилища(Гит);
	
	ЗапуститьПриложение(Команда, ПараметрыГита.Каталог, Истина);
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	ТекстовыйДокумент.Прочитать(ИмяВременногоФайла, КодировкаТекста.UTF8);
	
	СтрокаЗамены = ПараметрыГита.Проект + "/src/";
	
	МассивСтрок = Новый Массив;
	
	Для Сч = 1 По ТекстовыйДокумент.КоличествоСтрок() Цикл
		СтрокаФайл = ТекстовыйДокумент.ПолучитьСтроку(Сч);
		
		Если Не СтрНачинаетсяС(СтрЗаменить(СтрокаФайл, """", ""), СтрокаЗамены)
		ИЛИ СтрЗаканчиваетсяНа(СтрокаФайл, ".suppress") Тогда
			Продолжить;
		КонецЕсли;
			
		НоваяСтрока = Новый Структура("ИмяФайла");
		НоваяСтрока.ИмяФайла = СтрЗаменить(СтрокаФайл, СтрокаЗамены, "");
		
		МассивСтрок.Добавить(НоваяСтрока);
	КонецЦикла;
	
	Возврат МассивСтрок;
	
КонецФункции

// Команда для запуска выгрузки объектов.
// 
// Параметры:
//  ПараметрыВыполнения - см. РаботаСГитом.ПараметрыВыполнения
//  ПараметрыКонфигурации - см. РаботаСГитом.ПараметрыДляРаботыСКонфигурацией
//  МетаданныеКВыгрузке - Массив Из Строка
// 
// Возвращаемое значение:
//  Строка - Команда для запуска выгрузки объектов
Функция КомандаДляЗапускаВыгрузкиОбъектов(ПараметрыВыполнения, ПараметрыКонфигурации, МетаданныеКВыгрузке, Рекурсивно = Ложь) Экспорт
	ШаблонКоманды = """%1CBIN%\%1CVERSION%\bin\ibcmd.exe"" infobase config export objects %R% %OBJECTS_LIST% --out=""%WORKING_DIR%\bd"" " +
		"--db-server=""%SQL_SERVER_NAME%"" --dbms=MSSQLServer --db-name=""%SQL_BD_NAME%"" --db-user=""%SQL_DB_USER%"" " + 
		"--db-pwd=""%SQL_DB_PWD%"" " + 
		"--user=""%1C_USER%"" --password=""%1C_PWD%""";
		
	СтрокаВыполнения = ЗаполнитьОбщиеПараметрыСтроки(ПараметрыВыполнения, ПараметрыКонфигурации, ШаблонКоманды);
	СтрокаМетаданных = СтрСоединить(МетаданныеКВыгрузке, " ");
	СтрокаВыполнения = СтрЗаменить(СтрокаВыполнения, "%OBJECTS_LIST%", СтрокаМетаданных);
	СтрокаВыполнения = СтрЗаменить(СтрокаВыполнения, "%R%", ?(Рекурсивно, "-r", ""));
	СтрокаВыполнения = ЗаполнитьОбщиеПараметрыСтроки(ПараметрыВыполнения, ПараметрыКонфигурации, СтрокаВыполнения);
	
	Возврат СтрокаВыполнения;
КонецФункции   

Функция КомандаДляЗапускаЗагрузкиОбъектовBICMD(ПараметрыВыполнения, ПараметрыКонфигурации, ФайлыКЗагрузке) Экспорт
	ШаблонКоманды = """%1CBIN%\%1CVERSION%\bin\ibcmd.exe"" infobase config import files " +
	"--base-dir=""%WORKING_DIR%\bd"" ""%LIST_FILES%"" " +
	"--db-server=""%SQL_SERVER_NAME%"" --dbms=MSSQLServer --db-name=""%SQL_BD_NAME%"" --db-user=""%SQL_DB_USER%"" " + 
	"--db-pwd=""%SQL_DB_PWD%"" " + 
	"--user=""%1C_USER%"" --password=""%1C_PWD%""";
	
	СтрокаВыполнения = ЗаполнитьОбщиеПараметрыСтроки(ПараметрыВыполнения, ПараметрыКонфигурации, ШаблонКоманды);
	СтрокаФайлыКЗагрузке = СтрСоединить(ФайлыКЗагрузке, " ");
	СтрокаВыполнения = СтрЗаменить(СтрокаВыполнения, "%LIST_FILES%", СтрокаФайлыКЗагрузке);
	
	Возврат СтрокаВыполнения;
КонецФункции


Процедура ДобавитьВыводВФайл(СтрокаВыполнения, ПараметрыВыполнения, ИмяФайла) Экспорт
	СтрокаВыполнения = "cmd /C ""chcp 65001 &&" + СтрокаВыполнения + """ > " + ИмяФайла;
КонецПроцедуры


// Команда для запуска захвата объектов.
// 
// Параметры:
//  ПараметрыВыполнения - см. РаботаСГитом.ПараметрыВыполнения
//  ПараметрыКонфигурации - см. РаботаСГитом.ПараметрыДляРаботыСКонфигурацией
//  МетаданныеКВыгрузке - Массив Из Строка
// 
// Возвращаемое значение:
//  Строка - Команда для запуска выгрузки объектов
Функция КомандаДляЗапускаЗахватаОбъектов(ПараметрыВыполнения, ПараметрыКонфигурации, МетаданныеКВыгрузке) Экспорт
	ШаблонКоманды = """%1CBIN%\%1CVERSION%\bin\ibcmd.exe"" config export objects %OBJECTS_LIST% --out ""%WORKING_DIR%\bd"" " +
		"--db-server=""%SQL_SERVER_NAME%"" --dbms=MSSQLServer --db-name""%SQL_BD_NAME%"" --db-user=""%SQL_DB_USER%""";
		
	СтрокаВыполнения = ЗаполнитьОбщиеПараметрыСтроки(ПараметрыВыполнения, ПараметрыКонфигурации, ШаблонКоманды);
	СтрокаМетаданных = СтрСоединить(МетаданныеКВыгрузке, " ");
	СтрокаВыполнения = СтрЗаменить(СтрокаВыполнения, "%OBJECTS_LIST%", СтрокаМетаданных);
	СтрокаВыполнения = ЗаполнитьОбщиеПараметрыСтроки(ПараметрыВыполнения, ПараметрыКонфигурации, СтрокаВыполнения);
	
	Возврат СтрокаВыполнения;
КонецФункции

// Заполнить общие параметры строки.
// 
// Параметры:
//  ПараметрыВыполнения - см. РаботаСГитом.ПараметрыВыполнения
//  ПараметрыКонфигурации - см. РаботаСГитом.ПараметрыДляРаботыСКонфигурацией
//  Шаблон - Строка - Шаблон
// 
// Возвращаемое значение:
//  Строка
Функция ЗаполнитьОбщиеПараметрыСтроки(ПараметрыВыполнения, ПараметрыКонфигурации, Знач Шаблон)
	Результат = Шаблон;
	
	
	Результат = СтрЗаменить(Результат, "%1CBIN%", ПараметрыВыполнения.КаталогПлатформ);
	Результат = СтрЗаменить(Результат, "%1CVERSION%", ПараметрыКонфигурации.ВерсияПлатформы);
	Результат = СтрЗаменить(Результат, "%WORKING_DIR%", ПараметрыВыполнения.РабочийКаталог);
	Результат = СтрЗаменить(Результат, "%SQL_SERVER_NAME%", ПараметрыКонфигурации.СКЛСервер);
	Результат = СтрЗаменить(Результат, "%SQL_BD_NAME%", ПараметрыКонфигурации.СКЛИмяБазыДанных);
	Результат = СтрЗаменить(Результат, "%SQL_DB_USER%", ПараметрыКонфигурации.СКЛЛогин);
	Результат = СтрЗаменить(Результат, "%SQL_DB_PWD%", ПараметрыКонфигурации.СКЛПароль);
	
	Результат = СтрЗаменить(Результат, "%1C_USER%", ПараметрыКонфигурации.КонфигурацияПользователь);
	Результат = СтрЗаменить(Результат, "%1C_PWD%", ПараметрыКонфигурации.КонфигурацияПароль);
	
	Возврат Результат;
КонецФункции
