

#Область ПрограммныйИнтерфейс

// Получить описание хранилища.
// 
// Параметры:
//  ГитХранилище Гит хранилище
// 
// Возвращаемое значение:
//  Структура - Получить описание хранилища:
// * Каталог - Строка -
// * Проект -  - 
Функция ПолучитьОписаниеХранилища(ГитХранилище) Экспорт
	Результат = Новый Структура("Каталог", ГитХранилище.Каталог);
	Результат.Вставить("Проект", ГитХранилище.Проект);   
	
	Возврат Результат;
КонецФункции

// Параметры для работы с конфигурацией.
// 
// Параметры:         
//  Проект - СправочникСсылка.Проекты - Проект
//  Конфигурация - СправочникСсылка.Конфигурации - Конфигурация
// 
// Возвращаемое значение:
//  Структура - Параметры для работы с конфигурацией:
// * КонфигурацияФайловая - Булево 
// * КонфигурацияПуть - Строка
// * КонфигурацияПароль - Строка
// * ХранилищеПодключено - Булево
// * ХранилищеПуть -  Строка
// * ХранилищеПользователь - Строка
// * ХранилищеПароль - Строка
// * СКЛСервер - Строка
// * СКЛЛогин - Строка
// * СКЛПароль - Строка
// * ВерсияПлатформы - Строка
// * СКЛИмяБазыДанных - Строка
// * КонфигурацияПользователь - Строка 
// * ИмяРасширения - Строка 
// * ЭтоРасширение - Булево 
Функция ПараметрыДляРаботыСКонфигурацией(Конфигурация, Проект = Неопределено) Экспорт
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	Конфигурации.Файловая КАК Файловая,
	|	Конфигурации.ПутьКБазе КАК ПутьКБазе,
	|	Конфигурации.Пользователь КАК Пользователь,
	|	Конфигурации.Пароль КАК Пароль,
	|	Конфигурации.ПодключенаКХранилищу КАК ПодключенаКХранилищу,
	|	Конфигурации.УдалитьПутьКХранилищу КАК ПутьКХранилищу,
	|	Конфигурации.УдалитьПользовательХранилища КАК ПользовательХранилища,
	|	Конфигурации.УдалитьПарольХранилища КАК ПарольХранилища,
	|	Конфигурации.СерверСКЛ.Сервер КАК СерверСКЛСервер,
	|	Конфигурации.СерверСКЛ.Логин КАК СерверСКЛЛогин,
	|	Конфигурации.СерверСКЛ.Пароль КАК СерверСКЛПароль,
	|	Конфигурации.ВерсияПлатформы КАК ВерсияПлатформы,
	|	Конфигурации.СерверСКЛ.Наименование КАК СерверСКЛНаименование,
	|	ЕСТЬNULL(ГитХранилища.ЭтоРасширение, ЛОЖЬ) КАК ЭтоРасширение,
	|	ЕСТЬNULL(ГитХранилища.ИмяРасширения, """") КАК ИмяРасширения,
	|	Конфигурации.Ссылка КАК КонфигурацияСсылка
	|ПОМЕСТИТЬ ВТОписаниеКонфигурации
	|ИЗ
	|	Справочник.Конфигурации КАК Конфигурации,
	|	Справочник.Проекты КАК ГитХранилища
	|ГДЕ
	|	Конфигурации.Ссылка = &Конфигурация
	|	И ГитХранилища.Ссылка = &Проект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОписаниеКонфгирации.Файловая КАК Файловая,
	|	ВТОписаниеКонфгирации.ПутьКБазе КАК ПутьКБазе,
	|	ВТОписаниеКонфгирации.Пользователь КАК Пользователь,
	|	ВТОписаниеКонфгирации.Пароль КАК Пароль,
	|	ВЫБОР
	|		КОГДА ПодключенияКХранилищам.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПодключенаКХранилищу,
	|	ЕСТЬNULL(ПодключенияКХранилищам.ПутьКХранилищу, """") КАК ПутьКХранилищу,
	|	ЕСТЬNULL(ПодключенияКХранилищам.ПользовательХранилища, """") КАК ПользовательХранилища,
	|	ЕСТЬNULL(ПодключенияКХранилищам.ПарольХранилища, """") КАК ПарольХранилища,
	|	ВТОписаниеКонфгирации.СерверСКЛСервер КАК СерверСКЛСервер,
	|	ВТОписаниеКонфгирации.СерверСКЛЛогин КАК СерверСКЛЛогин,
	|	ВТОписаниеКонфгирации.СерверСКЛПароль КАК СерверСКЛПароль,
	|	ВТОписаниеКонфгирации.ВерсияПлатформы КАК ВерсияПлатформы,
	|	ВТОписаниеКонфгирации.СерверСКЛНаименование КАК СерверСКЛНаименование,
	|	ВТОписаниеКонфгирации.ЭтоРасширение КАК ЭтоРасширение,
	|	ВТОписаниеКонфгирации.ИмяРасширения КАК ИмяРасширения
	|ИЗ
	|	ВТОписаниеКонфигурации КАК ВТОписаниеКонфгирации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПодключенияКХранилищам КАК ПодключенияКХранилищам
	|		ПО ВТОписаниеКонфгирации.КонфигурацияСсылка = ПодключенияКХранилищам.Владелец
	|			И (ПодключенияКХранилищам.СвязанныйПроект = &Проект)";
	Запрос.УстановитьПараметр("Конфигурация", Конфигурация);
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Результат = Новый Структура();
	Результат.Вставить("КонфигурацияФайловая", Выборка.Файловая);
	Результат.Вставить("КонфигурацияПуть", Выборка.ПутьКБазе);
	Результат.Вставить("КонфигурацияПользователь", Выборка.Пользователь);
	Результат.Вставить("КонфигурацияПароль", Выборка.Пароль);
	Результат.Вставить("ХранилищеПодключено", Выборка.Пароль);
	Результат.Вставить("ХранилищеПуть", Выборка.ПутьКХранилищу);
	Результат.Вставить("ХранилищеПользователь", Выборка.ПользовательХранилища);
	Результат.Вставить("ХранилищеПароль", Выборка.ПарольХранилища);
	Результат.Вставить("СКЛСервер", Выборка.СерверСКЛСервер);
	Результат.Вставить("СКЛИмяБазыДанных", Выборка.СерверСКЛНаименование);
	Результат.Вставить("СКЛЛогин", Выборка.СерверСКЛЛогин);
	Результат.Вставить("СКЛПароль", Выборка.СерверСКЛПароль);
	Результат.Вставить("ВерсияПлатформы", Выборка.ВерсияПлатформы);
	
	Результат.Вставить("ЭтоРасширение", Выборка.ЭтоРасширение);
	Результат.Вставить("ИмяРасширения", Выборка.ИмяРасширения);
	
	Возврат Результат;
КонецФункции

// Параметры выполнения.
// 
// Возвращаемое значение:
//  Структура - Параметры выполнения:
// * РабочийКаталог - Произвольный, Строка -
// * КаталогПлатформ - Произвольный, Строка -
Функция ПараметрыВыполнения() Экспорт
	Результат = Новый Структура();
	
	Результат.Вставить("РабочийКаталог", Константы.РабочаяПапка.Получить());
	Результат.Вставить("КаталогПлатформ", Константы.КаталогПлатформ.Получить());
	
	Возврат Результат;
КонецФункции
#КонецОбласти

