

#Область ПрограммныйИнтерфейс

&НаСервере
Функция РазобратьФайл(Поток) Экспорт
	ЧтениеХМЛ = Новый ЧтениеXML;
	ЧтениеХМЛ.ОткрытьПоток(Поток);
	
	МассивИмен = Новый Массив;
	Уровень = 0;
	НаНужномУровне = Ложь;
	
	КорневойКэш = Новый Соответствие;  
	
	ЗначенияИсключения = Новый Массив;
	ЗначенияИсключения.Добавить("Resource");
	ЗначенияИсключения.Добавить("Dimension");
	ЗначенияИсключения.Добавить("Attribute");
	ЗначенияИсключения.Добавить("Help");
	ЗначенияИсключения.Добавить("TabularSection");
	
	КорневойКэш["ЗначенияИсключения"] = ЗначенияИсключения;
	
	Дерево = Новый ДеревоЗначений;      
	Дерево.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
	Дерево.Колонки.Добавить("ПолноеИмя", Новый ОписаниеТипов("Строка"));
	Дерево.Колонки.Добавить("Выбран", Новый ОписаниеТипов("Булево"));
	
	Пока ЧтениеХМЛ.Прочитать() Цикл
		Если ЧтениеХМЛ.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			Уровень = Уровень + 1;
			
			Если Уровень = 3 Тогда
				НаНужномУровне = Истина;         
				Пока ЧтениеХМЛ.ПрочитатьАтрибут() Цикл
					Если ЧтениеХМЛ.Имя = "name" Тогда
						ДобавитьВДеревоЗначение(ЧтениеХМЛ.Значение, Дерево, КорневойКэш);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		ИначеЕсли ЧтениеХМЛ.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			Уровень = Уровень - 1;
			НаНужномУровне = Ложь;
		КонецЕсли
		
	КонецЦикла;    
	ЧтениеХМЛ.Закрыть();

	Возврат Дерево;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьВДеревоЗначение(Знач ПереданноеЗначение, Дерево, Знач КорневойКэш)
	РазобранноеЗначениеМассив = СтрРазделить(ПереданноеЗначение, ".", Ложь);
	#Если Сервер И Не Сервер Тогда
		Дерево = Новый ДеревоЗначений;
	#КонецЕсли
	
	ИтоговаяСтрока = Неопределено;
	
	ТекущийУровеньСтроки = Дерево.Строки;
	ТекущийКэш = КорневойКэш;
	
	ЗначенияИсключения = КорневойКэш["ЗначенияИсключения"];
	
	Для Сч = 0 По РазобранноеЗначениеМассив.ВГраница() Цикл
		Значение = РазобранноеЗначениеМассив[Сч];          
		
		Если ЗначенияИсключения.Найти(Значение) <> Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		НайденнаяСтруктура = ТекущийКэш[Значение];
		
		Если НайденнаяСтруктура = Неопределено Тогда
			НоваяСтрока = ТекущийУровеньСтроки.Добавить();
			НоваяСтрока.Имя = Значение;
			НоваяСтрока.ПолноеИмя = ПереданноеЗначение;
			
			ТекущийУровеньСтроки = НоваяСтрока.Строки;  
			СтруктураКэш = СтруктураКэш(НоваяСтрока);
			ТекущийКэш[Значение] = СтруктураКэш;
			
			ТекущийКэш = СтруктураКэш.Кэш;
		Иначе
			ТекущийУровеньСтроки = НайденнаяСтруктура.Строка.Строки;
			ТекущийКэш = НайденнаяСтруктура.Кэш;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры 

&НаСервере
Функция СтруктураКэш(СтрокаДерева)
	Результат = Новый Структура;
	Результат.Вставить("Строка", СтрокаДерева);
	Результат.Вставить("Кэш", Новый Соответствие);
	
	Возврат Результат;
КонецФункции



#КонецОбласти