
#Область ПрограммныйИнтерфейс

Процедура СообщитьПользователю(Текст) Экспорт
	Сообщение = Новый СообщениеПользователю();
	Сообщение.Текст = Текст;
	Сообщение.Сообщить();
КонецПроцедуры

// Возвращает копию исходного массива с уникальными значениями.
//
// Параметры:
//  Массив - Массив - массив произвольных значений.
//
// Возвращаемое значение:
//  Массив - массив уникальных элементов.
//
Функция СвернутьМассив(Знач Массив) Экспорт
	Результат = Новый Массив;
	ДополнитьМассив(Результат, Массив, Истина);
	Возврат Результат;
КонецФункции

// Дополняет массив МассивПриемник значениями из массива МассивИсточник.
//
// Параметры:
//  МассивПриемник - Массив - массив, в который необходимо добавить значения.
//  МассивИсточник - Массив - массив значений для заполнения.
//  ТолькоУникальныеЗначения - Булево - если истина, то в массив будут включены только уникальные значения.
//
Процедура ДополнитьМассив(МассивПриемник, МассивИсточник, ТолькоУникальныеЗначения = Ложь) Экспорт
	
	Если ТолькоУникальныеЗначения Тогда
		
		УникальныеЗначения = Новый Соответствие;
		
		Для Каждого Значение Из МассивПриемник Цикл
			УникальныеЗначения.Вставить(Значение, Истина);
		КонецЦикла;
		
		Для Каждого Значение Из МассивИсточник Цикл
			Если УникальныеЗначения[Значение] = Неопределено Тогда
				МассивПриемник.Добавить(Значение);
				УникальныеЗначения.Вставить(Значение, Истина);
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		Для Каждого Значение Из МассивИсточник Цикл
			МассивПриемник.Добавить(Значение);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Найти строки в дереве.
// 
// Параметры:
//  ДанныеДерева - ДеревоЗначений, ДанныеФормыДерево - Данные дерева
//  Отбор - Структура - Отбор
//  ИскатьВПодстроках - Булево - Искать в подстроках
//  ИскатьВсеЭлементы - Булево - Искать все элементы
// 
// Возвращаемое значение:
//  Массив из СтрокаДереваЗначений - Результаты поиска
Функция НайтиСтрокиВДереве(ДанныеДерева, Отбор, ИскатьВПодстроках = Истина, ИскатьВсеЭлементы = Истина) Экспорт
	
	ТипДереваЗначений = Неопределено;
	#Если Сервер Тогда
		ТипДереваЗначений = Тип("ДеревоЗначений");
	#КонецЕсли
	
	//Вариант для модулей
	Если ТипЗнч(ДанныеДерева) = ТипДереваЗначений Тогда
		//
		РезультатПоиска = ДанныеДерева.Строки.НайтиСтроки(Отбор, ИскатьВПодстроках);
		Если ИскатьВсеЭлементы Тогда
			Возврат РезультатПоиска;
		Иначе 
			Если РезультатПоиска.Количество() > 1 Тогда 
				ПервыйЭлементПоиска = РезультатПоиска[0];
				Возврат ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПервыйЭлементПоиска);
			Иначе 
				Возврат РезультатПоиска;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	//Вариант для форм
	МассивНайденныхЭлементов = Новый Массив;
	Для Каждого СтрокаДерева Из ДанныеДерева.ПолучитьЭлементы() Цикл
		
		Найдено = Истина;
		Для Каждого ЭлементОтбора Из Отбор Цикл
			Если СтрокаДерева[ЭлементОтбора.Ключ] <> ЭлементОтбора.Значение Тогда
				Найдено = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		Если Найдено Тогда
			МассивНайденныхЭлементов.Добавить(СтрокаДерева);
			Если НЕ ИскатьВсеЭлементы Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
		
		Если ИскатьВПодстроках Тогда
			РезультатПоиска = НайтиСтрокиВДереве(СтрокаДерева, Отбор, ИскатьВПодстроках, ИскатьВсеЭлементы);
			Если РезультатПоиска <> Неопределено Тогда
				Если НЕ ИскатьВсеЭлементы 
				И РезультатПоиска.Количество() > 0 Тогда
					Возврат РезультатПоиска;
				Иначе 
					Для Каждого НайденныйЭлемент Из РезультатПоиска Цикл
						МассивНайденныхЭлементов.Добавить(НайденныйЭлемент);
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	
	Возврат МассивНайденныхЭлементов;
	
	
КонецФункции

Процедура ДобавитьУникальноеЗначениеВМассив(Массив, Значение) Экспорт
	Если Массив.Найти(Значение) = Неопределено Тогда
		Массив.Добавить(Значение);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти