
&НаСервере
Функция СформироватьФайлДляХранилищаНаСервере(Знач ВыделенныеСтроки)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КоммитыИзмененныеФайлы.ИмяМетаданных КАК ИмяМетаданных
	|ИЗ
	|	Справочник.Коммиты.ИзмененныеФайлы КАК КоммитыИзмененныеФайлы
	|ГДЕ
	|	КоммитыИзмененныеФайлы.Ссылка В (&Коммиты)
	|
	|СГРУППИРОВАТЬ ПО
	|	КоммитыИзмененныеФайлы.ИмяМетаданных";
	Запрос.УстановитьПараметр("Коммиты", ВыделенныеСтроки);
	
	МетаданныеКВыгрузке = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИмяМетаданных");
	
	ЗаписьХМЛ = Новый ЗаписьXML;      
	ВремФайл = ПолучитьИмяВременногоФайла();
	
	ЗаписьХМЛ.ОткрытьФайл(ВремФайл);
	ЗаписьХМЛ.ЗаписатьНачалоЭлемента("Objects");
	ЗаписьХМЛ.ЗаписатьАтрибут("xmlns", "http://v8.1c.ru/8.3/config/objects");
	ЗаписьХМЛ.ЗаписатьАтрибут("version", "1.0");
	
	Для Каждого ИмяМетаданных ИЗ МетаданныеКВыгрузке Цикл		
		Если ИмяМетаданных = "Configuration" Тогда
			ЗаписьХМЛ.ЗаписатьНачалоЭлемента("Configuration");
			ЗаписьХМЛ.ЗаписатьАтрибут("includeChildObjects", "false");
			ЗаписьХМЛ.ЗаписатьКонецЭлемента();
			Продолжить;
		КонецЕсли;
		
		ЗаписьХМЛ.ЗаписатьНачалоЭлемента("Object");
		ЗаписьХМЛ.ЗаписатьАтрибут("fullName", ИмяМетаданных);
		ЗаписьХМЛ.ЗаписатьАтрибут("includeChildObjects", "true");
		ЗаписьХМЛ.ЗаписатьКонецЭлемента();
		
	КонецЦикла;    
	ЗаписьХМЛ.ЗаписатьКонецЭлемента();
	ЗаписьХМЛ.Закрыть();
	
	Возврат Новый ДвоичныеДанные(ВремФайл);
	
	
КонецФункции

&НаКлиенте
Асинх Процедура СформироватьФайлДляХранилища(Команда)
	Файл = СформироватьФайлДляХранилищаНаСервере(Элементы.Список.ВыделенныеСтроки);
	
	ДиалогСохранения = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Результат = Ждать ДиалогСохранения.ВыбратьАсинх();
	
	Если Результат <> Неопределено Тогда
		Файл.Записать(ДиалогСохранения.ПолноеИмяФайла);
	КонецЕсли;
	
КонецПроцедуры
