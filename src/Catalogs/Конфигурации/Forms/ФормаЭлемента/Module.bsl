
&НаКлиенте
Асинх Процедура ВыгрузитьКонфигДампИнфо(Команда)
	Если Модифицированность Тогда
		Ответ = Ждать ВопросАсинх("Для продолжения необходимо записать. Продолжить?", РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Записать();
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыВыполнения = РаботаСГитомВызовСервера.ПараметрыВыполнения();
	ПараметрыКонфигурации = РаботаСГитомВызовСервера.ПараметрыДляРаботыСКонфигурацией(Объект.Ссылка);
	КомандаКЗапуску = РаботаСКонфигурациейКлиентСервер.КомандаДляСозданияКонфигДампИнфо(ПараметрыВыполнения, 
		ПараметрыКонфигурации);
	
	ВремФайл = ПолучитьИмяВременногоФайла();
	РаботаСКонфигурациейКлиентСервер.ДобавитьВыводВФайл(КомандаКЗапуску, ПараметрыВыполнения, ВремФайл);
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КомандаКЗапуску);
	
	Попытка
		ЗапуститьПриложение(КомандаКЗапуску);
	Исключение
		УдалитьФайлы(ВремФайл);
		ВызватьИсключение;
	КонецПопытки;
	
	УдалитьФайлы(ВремФайл);
	
КонецПроцедуры
